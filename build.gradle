// Top-level build file where you can add configuration options common to all sub-projects/modules.
import io.embrace.gradle.Versions

buildscript {
    repositories {
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:${Versions.agp}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}")
        classpath("io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${Versions.detekt}")
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:${Versions.dokka}")
        classpath("org.jetbrains.dokka:android-documentation-plugin:${Versions.dokka}")
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
}

group = "io.embrace"
version = project.properties.version

// load credentials from local properties if present
def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USERNAME") ?: localProperties.ossrhUsername
            password = System.getenv("SONATYPE_PASSWORD") ?: localProperties.ossrhPassword
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
        }
    }
}

allprojects {
    repositories {
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}